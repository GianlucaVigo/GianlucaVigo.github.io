# 2024-01-25 FLC Lab exam: repeat_exp statement

## New feature
This [exam](https://github.com/GianlucaVigo/acse/tree/24-01-25) is requesting the definition of the **repeat_exp statement**

## How does the feature work?
The **repeat_exp** statement has the following syntax:

> **repeat_exp** (_var_ = _exp1_, _exp2_, _exp3_);

The **repeat_exp** statement implements a simple loop which, at **every iteration**, re-computes _exp3_ and assigns it to _var_.

_var_ is initialized to the value of _exp1_ **before the loop**.

The number of iterations is controlled by _exp2_.


NB:
* if _exp2_ is **zero or negative**, the loop should never iterate, leaving _var_ to its initial value (= _exp1_)
* assume that _exp2_ does not use _var_ in its definition -> _exp2_ **does not change** during the execution of the loop

## Code example
```c
int  a, b;

repeat_exp(a=0, 10, a+1); // a == 10

write(a);

repeat_exp(b=a*3, a-15, b-1); // b == 30

write(b);

repeat_exp(a=1000, 3, (a+4096576/a)/2); // a == 2024

write(a);
```

***

* The first **repeat_exp** statement initializes `a = 0`, the performs the assignment 
`a <- a+1`
ten times (as specified by _exp2_) leaving 
`a = 10`
at the end.

* The second **repeat_exp** statement initializes `b = a * 3 // b = 30`, but then _exp2_ evaluates to `-5` (**negative value case**). So that b is left to 30.

* The third **repeat_exp** statement is another case (slightly more horrible) to consider.

## Resolution
**scanner.l**

I need to include the **repeat_exp** statement new token: in particular the REPEAT_EXP token, so that it can be recognized.

```c
"repeat_exp"              { return REPEAT_EXP; }
```

whenever in a program the scanner sees **repeat_exp**, it will return REPEAT_EXP.

***

**parser.h**

I need to associate to the REPEAT_EXP token a semantic value. 

This means that I'm associating to the REPEAT_EXP token two informations:
* end label
* startLoop label
that will always be necessary whenever a repeat_exp statement is inserted in the code.

```c
typedef struct {
  t_label *end;
  t_label *startLoop;
} t_repeat_expStmt;
```

***

**parser.y**

In the parser file there will be the majority of the work.

1) union declaration

```c
%union {
  int integer;
  char *string;
  t_regID reg;
  t_symbol *var;
  t_listNode *list;
  t_label *label;
  t_ifStmt ifStmt;
  t_whileStmt whileStmt;

  t_repeat_expStmt repeat_expStmt;

}
```

through this declaration we can use "repeat_expStmt" as an alternative of "t_repeat_expStmt" in the code.

2) token declaration

Since the REPEAT_EXP token has a semantic value, I specify it in an other section:

```C
// These are the tokens with a semantic value.
%token <ifStmt> IF
%token <whileStmt> WHILE
%token <label> DO
%token <string> IDENTIFIER
%token <integer> NUMBER

%token <repeat_expStmt> REPEAT_EXP
```

3) Insert the REPEAT_EXP statement in the **list of statements** available.

```C
statement
  : assign_statement SEMI
  | if_statement
  | while_statement
  | do_while_statement SEMI
  | return_statement SEMI
  | read_statement SEMI
  | write_statement SEMI

  | repeat_exp_statement SEMI

  | SEMI
;
```

4) Since the **repeat_exp** is a statement, we need to inserted it from scratch.

```c
// 24-01-25 exam: repeat exp statement
repeat_exp_statement
  : REPEAT_EXP LPAR var_id ASSIGN exp COMMA exp
  {
    // variable initialization (var = exp1)
    genStoreRegisterToVariable(program, $3, $5);
    
    // define the "end" label associated to REPEAT_EXP token
    $1.end = createLabel(program);

    // if exp2 <= 0, jump to the end of the program
    genBLE(program, $7, REG_0, $1.end);

    // initializing the counter (coming from exp2)
    t_regID counter = getNewRegister(program);
    genADD(program, counter, $7, REG_0);

    // define the "startLoop" label associated to REPEAT_EXP token + assignment
    $1.startLoop = createLabel(program);
    assignLabel(program, $1.startLoop);

    // if counter == 0 -> jump to "end" label 
    genBEQ(program, counter, REG_0, $1.end);

    // update counter value (counter--)
    genSUBI(program, counter, counter, 1);

  } COMMA exp RPAR {

    // var = exp3
    genStoreRegisterToVariable(program, $3, $10);

    // jump to startLoop label
    genJ(program, $1.startLoop);

    // "end" label assignment
    assignLabel(program, $1.end);
  }
;
```

## Visual Representation

![240125-VisualRep](https://github.com/user-attachments/assets/5302c8e7-bdd1-460e-9430-7dd179ccb7ec)

## Testing

1) run the command `make all` in order to let the compiler be aware about the new features/modifications

2) added the [tests/repeat_exp/repeat_exp.c](#code-example) file

3) run the command `./bin/acse ./tests/repeat_exp/repeat_exp.c -o ./tests/repeat_exp/repeat_exp.asm` to translate c code into assembly

4) run the command `./bin/asrv32im ./tests/repeat_exp/repeat_exp.asm -o ./tests/repeat_exp/repeat_exp.o` to translate assembly file into an object one

5) run the command `./bin/simrv32im ./tests/repeat_exp/repeat_exp.o` to run the object file

6) see the results

![240125_Result](https://github.com/user-attachments/assets/ae79f2b7-489a-42fb-b62f-05c50bac7e7c)

## NB

* there's no explicit method that gives the possibility to store a value of a register into an other one, so that's why when we need to store in the result register ($$) the value contained in, for instance, register 5 ($5), we perform an ADDI/ADD operation in which one addend is 0/REG_0 and the second one $5.

> `$$ = $5 + REG_0 (or 0)`

* here's an image to show how the register naming works:

![240125-RegNums](https://github.com/user-attachments/assets/3eb04af7-7be8-4c93-9e72-292391a70c4d)

* labels require first the **creation**

> `LABELNAME = createLabel(program)`

 then the **assignment**

> `assignLabel(program, LABELNAME)`

* In this case, the "REPEAT_EXP" token has a related struct: whenever we have to access its fields, I can use the dotted notation. 

Why do we need the struct? 
In order to be able to refers to labels defined in different scopes/blocks! In fact:

  ** end label: in the first block I create it and I assign it in the second one. There are jump instructions that start from one block and land in the other one. 

  ** startLoop: in the first block I create and assign it. There are jump instructions that start from one block and land in the other one.

So that in both cases, I can't solve this issue by using common labels but they must be related to a common element which is the REPEAT_EXP ($1) token.
